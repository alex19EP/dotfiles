# Source zstyles you might use with antidote.
[[ -e ${ZDOTDIR:-~}/.zstyles ]] && source ${ZDOTDIR:-~}/.zstyles

# antidote
[[ -e ${ZDOTDIR:-~}/.antidote/antidote.zsh ]] && source ${ZDOTDIR:-~}/.antidote/antidote.zsh

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# initialize plugins statically with ${ZDOTDIR:-~}/.zsh_plugins.txt
antidote load

# aliases
[ -f "${ZDOTDIR}/aliases" ] && source "${ZDOTDIR}/aliases" ||:

HISTFILE="$XDG_STATE_HOME"/zsh/history
WORDCHARS="${WORDCHARS/\//}"

# zstyle overrides (base set by zephyr compstyle)
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}'
zstyle ':completion:*' special-dirs false
zstyle ':completion:*' max-errors 1
zstyle ':completion:*' use-compctl true
zstyle ':completion:*' list-grouped true
zstyle ':completion:*' list-suffixes true
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*:functions' ignored-patterns '_*'
zstyle ':completion:*:*:-command-:*:*' ignored-patterns '_*'

autoload -Uz up-line-or-beginning-search down-line-or-beginning-search edit-command-line
autoload -Uz run-help-git run-help-ip run-help-openssl run-help-sudo run-help-svn

# code
VSCODE={{ template "vscode" }}

setopt COMPLETE_ALIASES GLOB_COMPLETE COMPLETE_IN_WORD LIST_PACKED
setopt auto_param_slash     # if completed parameter is a directory, add a trailing slash
TRAPUSR1() { rehash }

# Do not offer completion functions as corrections
CORRECT_IGNORE='_*'

# History overrides (base set by zephyr history)
setopt HIST_NO_STORE
setopt INC_APPEND_HISTORY_TIME

# Prompting
setopt PROMPT_SUBST

setopt NOMATCH

## Key bindings
# Use Emacs-like keybindings
bindkey -e

# Shift+Tab: reverse menu completion
bindkey "$terminfo[kcbt]" reverse-menu-complete

# search in history
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "$terminfo[kcuu1]" up-line-or-beginning-search
bindkey "$terminfo[kcud1]" down-line-or-beginning-search

# prepend sudo (Esc Esc)
bindkey '\e\e' prepend-sudo

# edit cmd
zle -N edit-command-line
bindkey "^X^E" edit-command-line

function expand-alias-and-accept-line() {
  zle _expand_alias
  zle accept-line
}

zle -N expand-alias-and-accept-line
bindkey '^M' expand-alias-and-accept-line  # Enter

if (( $+commands[atuin] )); then
    eval "$(atuin init zsh --disable-up-arrow)"
fi
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.config/p10k/p10k.zsh ]] || source ~/.config/p10k/p10k.zsh
